apply plugin: 'idea'
apply plugin: 'java'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

apply plugin: 'application'
mainClassName = 'com.revolut.interview.TransferServiceStarter'

ext {
    guice = '4.2.2'
    log4j2 = '2.12.1'
    hibernate = '5.4.4.Final'
    junit5 = '5.6.0'
    mockito = '3.2.4'
    javalin = '3.7.0'
    h2 = '1.4.200'
    restAssured = '4.2.0'
    spark = '2.8.0'
    gson = '2.8.6'
}

repositories {
    mavenCentral()
}

sourceSets {
    intTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    intTestImplementation.extendsFrom testImplementation
    intTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    implementation "org.apache.logging.log4j:log4j-api:$log4j2"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:$log4j2"
    runtimeOnly "org.apache.logging.log4j:log4j-core:$log4j2"

    implementation "org.hibernate:hibernate-core:$hibernate"
    implementation "org.hibernate:hibernate-java8:$hibernate"
    runtimeOnly "org.hibernate:hibernate-hikaricp:$hibernate"

    runtimeOnly "com.h2database:h2:$h2"

    implementation "com.google.inject:guice:$guice"

    implementation "com.sparkjava:spark-core:$spark"
    implementation "com.google.code.gson:gson:$gson"

    testImplementation "io.rest-assured:rest-assured:$restAssured"
    testImplementation "io.rest-assured:json-path:$restAssured"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit5"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit5"

    testImplementation "org.mockito:mockito-core:$mockito"
    testImplementation "org.mockito:mockito-junit-jupiter:$mockito"
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
    shouldRunAfter test
}

check.dependsOn integrationTest

tasks.withType(Test) {
    useJUnitPlatform()
}
